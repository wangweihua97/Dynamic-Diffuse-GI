/*
  Computes indirect radiance at a surfel based on the surrounding probe cage.
  Uses helpers from the G3D innovation engine (http://g3d.sf.net)
*/

#version 420 // -*- c++ -*-

#extension GL_ARB_texture_query_lod : enable

#include <compatibility.glsl>
#include <g3dmath.glsl>
#include <LightingEnvironment/LightingEnvironment_uniforms.glsl>
#include <Light/Light.glsl>
#include <Texture/Texture.glsl>
#include <GBuffer/GBuffer.glsl>
#include <reconstructFromDepth.glsl>
#include <octahedral.glsl>
#include <noise.glsl>

#include "GridHelpers.glsl"

// Optional per-pixel buffers for computing the outgoing light ("view") vector.
// Compile away if unused.
uniform_Texture(sampler2D, gbuffer_WS_RAY_DIRECTION_);
uniform_Texture(sampler2D, gbuffer_WS_RAY_ORIGIN_);

// Declare a GBuffer with no prefix
uniform_GBuffer(gbuffer_);

uniform int probeSideLength;

// Combat the sensitive perception of very small amounts of light leaking
// and then recursively lighting closed rooms by losing energy with each shade
uniform float energyPreservation;

uniform IrradianceField irradianceFieldSurface;

out Color3 E_lambertianIndirect;
out Color3 E_glossyIndirect;

vec2 textureCoordFromDirection(vec3 dir, int probeIndex, int fullTextureWidth, int fullTextureHeight, int probeSideLength) {
    vec2 normalizedOctCoord = octEncode(normalize(dir));
    vec2 normalizedOctCoordZeroOne = (normalizedOctCoord + vec2(1.0f)) * 0.5f;

    // Length of a probe side, plus one pixel on each edge for the border
    float probeWithBorderSide = (float)probeSideLength + 2.0f;

    vec2 octCoordNormalizedToTextureDimensions = (normalizedOctCoordZeroOne * (float)probeSideLength) / vec2((float)fullTextureWidth, (float)fullTextureHeight);

    int probesPerRow = (fullTextureWidth - 2) / (int)probeWithBorderSide;

    // Add (2,2) back to texCoord within larger texture. Compensates for 1 pix 
    // border around texture and further 1 pix border around top left probe.
    vec2 probeTopLeftPosition = vec2(mod(probeIndex, probesPerRow) * probeWithBorderSide,
        (probeIndex / probesPerRow) * probeWithBorderSide) + vec2(2.0f, 2.0f);

    vec2 normalizedProbeTopLeftPosition = vec2(probeTopLeftPosition) / vec2((float)fullTextureWidth, (float)fullTextureHeight);

    return vec2(normalizedProbeTopLeftPosition + octCoordNormalizedToTextureDimensions);
}

void main() {
    // Screen-space point being shaded
    ivec2 C = ivec2(gl_FragCoord.xy);

    Vector3 wsN = texelFetch(gbuffer_WS_NORMAL_buffer, C, 0).xyz;

    if (dot(wsN, wsN) < 0.01) {
        E_glossyIndirect = Color3(0);
        E_lambertianIndirect = Color3(0);
        return;
    }
    
    Point3 wsPosition = texelFetch(gbuffer_WS_POSITION_buffer, C, 0).xyz;

    // View vector
    Vector3 w_o = normalize(texelFetch(gbuffer_WS_RAY_ORIGIN_buffer, C, 0).xyz - wsPosition);

    // Glossy coefficient in BSDF (this code unpacks
    // G3D::UniversalBSDF's encoding)
    Color4  F0 = texelFetch(gbuffer_GLOSSY_buffer, C, 0);

    float glossyExponent = smoothnessToBlinnPhongExponent(F0.a);

    float cos_o = dot(wsN, w_o);

    // Incoming reflection vector
    Vector3 w_mi = normalize(wsN * (2.0 * cos_o) - w_o);

    E_glossyIndirect = computeGlossyEnvironmentMapLighting(w_mi, (F0.a == 1.0), glossyExponent, false);

    ivec3 baseGridCoord = baseGridCoord(irradianceFieldSurface, wsPosition);
    Point3 baseProbePos = gridCoordToPosition(irradianceFieldSurface, baseGridCoord);
    Irradiance3 sumIrradiance = Irradiance3(0);
    float sumWeight = 0.0;

    // alpha is how far from the floor(currentVertex) position. on [0, 1] for each axis.
    Vector3 alpha = clamp((wsPosition - baseProbePos) / irradianceFieldSurface.probeStep, Vector3(0), Vector3(1));

    // Iterate over adjacent probe cage
    for (int i = 0; i < 8; ++i) {
        // Compute the offset grid coord and clamp to the probe grid boundary
        // Offset = 0 or 1 along each axis
        GridCoord  offset = ivec3(i, i >> 1, i >> 2) & ivec3(1);
        GridCoord  probeGridCoord = clamp(baseGridCoord + offset, GridCoord(0), GridCoord(irradianceFieldSurface.probeCounts - 1));
        ProbeIndex p = gridCoordToProbeIndex(irradianceFieldSurface, probeGridCoord);

        // Make cosine falloff in tangent plane with respect to the angle from the surface to the probe so that we never
        // test a probe that is *behind* the surface.
        // It doesn't have to be cosine, but that is efficient to compute and we must clip to the tangent plane.
        Point3 probePos = gridCoordToPosition(irradianceFieldSurface, probeGridCoord);

        // Bias the position at which visibility is computed; this
        // avoids performing a shadow test *at* a surface, which is a
        // dangerous location because that is exactly the line between
        // shadowed and unshadowed. If the normal bias is too small,
        // there will be light and dark leaks. If it is too large,
        // then samples can pass through thin occluders to the other
        // side (this can only happen if there are MULTIPLE occluders
        // near each other, a wall surface won't pass through itself.)
        Vector3 probeToPoint = wsPosition - probePos + (wsN + 3.0 * w_o) * irradianceFieldSurface.normalBias;
        Vector3 dir = normalize(-probeToPoint);

        // Compute the trilinear weights based on the grid cell vertex to smoothly
        // transition between probes. Avoid ever going entirely to zero because that
        // will cause problems at the border probes. This isn't really a lerp. 
        // We're using 1-a when offset = 0 and a when offset = 1.
        Vector3 trilinear = lerp(1.0 - alpha, alpha, offset);
        float weight = 1.0;

        // Clamp all of the multiplies. We can't let the weight go to zero because then it would be 
        // possible for *all* weights to be equally low and get normalized
        // up to 1/n. We want to distinguish between weights that are 
        // low because of different factors.

        // Smooth backface test
        {
            // Computed without the biasing applied to the "dir" variable. 
            // This test can cause reflection-map looking errors in the image
            // (stuff looks shiny) if the transition is poor.
            Vector3 trueDirectionToProbe = normalize(probePos - wsPosition);

            // The naive soft backface weight would ignore a probe when
            // it is behind the surface. That's good for walls. But for small details inside of a
            // room, the normals on the details might rule out all of the probes that have mutual
            // visibility to the point. So, we instead use a "wrap shading" test below inspired by
            // NPR work.
            // weight *= max(0.0001, dot(trueDirectionToProbe, wsN));

            // The small offset at the end reduces the "going to zero" impact
            // where this is really close to exactly opposite
            weight *= square(max(0.0001, (dot(trueDirectionToProbe, wsN) + 1.0) * 0.5)) + 0.2;
        }
        
        // Moment visibility test
        {
            vec2 texCoord = textureCoordFromDirection(-dir,
                p,
                irradianceFieldSurface.depthTextureWidth,
                irradianceFieldSurface.depthTextureHeight,
                irradianceFieldSurface.depthProbeSideLength);

            float distToProbe = length(probeToPoint);

            float2 temp = texture(irradianceFieldSurface.meanMeanSquaredProbeGridbuffer, texCoord, 0).rg;
            float mean = temp.x;
            float variance = abs(square(temp.x) - temp.y);

            // http://www.punkuser.net/vsm/vsm_paper.pdf; equation 5
            // Need the max in the denominator because biasing can cause a negative displacement
            float chebyshevWeight = variance / (variance + square(max(distToProbe - mean, 0.0)));
                
            // Increase contrast in the weight 
            chebyshevWeight = max(pow3(chebyshevWeight), 0.0);

            weight *= (distToProbe <= mean) ? 1.0 : chebyshevWeight;
        }

        // Avoid zero weight
        weight = max(0.000001, weight);
                 
        Vector3 irradianceDir = wsN;

        vec2 texCoord = textureCoordFromDirection(normalize(irradianceDir),
            p,
            irradianceFieldSurface.irradianceTextureWidth,
            irradianceFieldSurface.irradianceTextureHeight,
            irradianceFieldSurface.irradianceProbeSideLength);

        Irradiance3 probeIrradiance = texture(irradianceFieldSurface.irradianceProbeGridbuffer, texCoord).rgb;

        // A tiny bit of light is really visible due to log perception, so
        // crush tiny weights but keep the curve continuous. This must be done
        // before the trilinear weights, because those should be preserved.
        const float crushThreshold = 0.2;
        if (weight < crushThreshold) {
            weight *= weight * weight * (1.0 / square(crushThreshold)); 
        }

        // Trilinear weights
        weight *= trilinear.x * trilinear.y * trilinear.z;

        // Weight in a more-perceptual brightness space instead of radiance space.
        // This softens the transitions between probes with respect to translation.
        // It makes little difference most of the time, but when there are radical transitions
        // between probes this helps soften the ramp.
#       if LINEAR_BLENDING == 0
            probeIrradiance = sqrt(probeIrradiance);
#       endif
        
        sumIrradiance += weight * probeIrradiance;
        sumWeight += weight;
    }

    Irradiance3 netIrradiance = sumIrradiance / sumWeight;

    // Go back to linear irradiance
#   if LINEAR_BLENDING == 0
        netIrradiance = square(netIrradiance);
#   endif
    netIrradiance *= energyPreservation;

    E_lambertianIndirect = 0.5 * pi * netIrradiance;
}
