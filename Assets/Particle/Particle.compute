#pragma kernel Update NUM_THREAD_X=256

#include "Particle.hlsl"
#include "Packages/ga.fuquna.computeshaderbvhmeshhit/Bvh.hlsl"


//#define CHECK_HIT_TRIANGLE_ALL 1

RWStructuredBuffer<Particle> particleBuffer;
float3 spawnBoundsMin;
float3 spawnBoundsMax;
float gravity;
float damping;
float bounceRate;
float time; // for random
float deltaTime;

float xorShift(uint idx)
{
    Particle particle = particleBuffer[idx];
    uint state = particle.randState;

    state ^= (state << 13); 
    state ^= (state >> 17);
    state ^= (state << 5);

    particle.randState = state;
    particleBuffer[idx] = particle;


    const uint uint_max = 4294967295;
    return (float)state / uint_max;
}

float3 Respawn(uint idx)
{
    float3 rand3 = float3(
        xorShift(idx),
        xorShift(idx),
        xorShift(idx)
    );

    return lerp(spawnBoundsMin, spawnBoundsMax, rand3);
}

[numthreads(NUM_THREAD_X,1,1)]
void Update (uint3 id : SV_DispatchThreadID)
{
    uint num, stride;
    particleBuffer.GetDimensions(num, stride);
    if (num <= id.x) return;

    Particle particle = particleBuffer[id.x];
    float3 pos = particle.position;
    float3 velocity = particle.velocity;

    // Calc velocity
    velocity.y += -gravity * deltaTime;
    velocity *= pow(abs(damping), deltaTime);

    float3 step = velocity * deltaTime;

    // Check hit
    float rayScale;
    float3 normal;
#ifdef CHECK_HIT_TRIANGLE_ALL
    if ( LineTriangleIntersectionAll(particle.position, step, rayScale, normal) )
#else
    if ( TraverseBvh(pos, step, rayScale, normal) )
#endif
    {
        velocity = reflect(velocity, normal) * bounceRate;
        step = velocity * deltaTime;
    }


    // Respawn
    pos += step;
    if ( pos.y < -10){
        pos = Respawn(id.x);
        velocity = (0).xxx;
    }

    particle.position = pos;
    particle.velocity = velocity;

    particleBuffer[id.x] = particle;
}